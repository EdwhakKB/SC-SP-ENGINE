package scfunkin.objects.misc;

import flixel.addons.display.FlxPieDial;
#if (VIDEOS_ALLOWED && hxvlc)
import hxvlc.flixel.FlxVideoSprite;

@:structInit
@:publicFields
class VideoParams
{
  var name:String = '';

  @:optional
  var ext:String = 'mp4';

  @:optional
  var isWaiting:Bool = false;

  @:optional
  var canSkip:Bool = true;

  @:optional
  var loop:Bool = false;

  @:optional
  var playOnLoad:Bool = true;

  @:optional
  var adjustSize:Bool = true;

  @:optional
  var autoPause:Bool = true;

  @:optional
  var finishCallback:Void->Void;

  @:optional
  var skipCallback:Void->Void;
}

class VideoSprite extends FlxSpriteGroup
{
  public static var _videos:Array<VideoSprite> = [];

  public var finishCallback:Void->Void = null;
  public var onSkip:Void->Void = null;

  final _timeToSkip:Float = 1;

  public var holdingTime:Float = 0;
  public var videoSprite:FlxVideoSprite;
  public var skipSprite:FlxPieDial;
  public var cover:FlxSprite;
  public var canSkip(default, set):Bool = false;

  private var videoName:String;

  public var waiting:Bool = false;
  public var isPlaying:Bool = false;
  public var isPaused:Bool = false;

  public var removeFromPlayState:Bool = true;

  public function new(videoName:String, isWaiting:Bool, canSkip:Bool = false, shouldLoop:Dynamic = false, autoPause = true, adjustSize:Bool = true)
  {
    super();

    this.videoName = videoName;
    scrollFactor.set();
    cameras = [FlxG.cameras.list[FlxG.cameras.list.length - 1]];

    waiting = isWaiting;
    if (!waiting)
    {
      cover = new FlxSprite().makeGraphic(1, 1, FlxColor.BLACK);
      cover.scale.set(FlxG.width + 100, FlxG.height + 100);
      cover.screenCenter();
      cover.scrollFactor.set();
      add(cover);
    }

    // initialize sprites
    videoSprite = new FlxVideoSprite();
    videoSprite.antialiasing = Save.get('antialiasing');
    videoSprite.autoPause = autoPause;
    add(videoSprite);
    this.canSkip = canSkip;

    // callbacks
    if (!shouldLoop) videoSprite.bitmap.onEndReached.add(destroy);

    if (adjustSize)
    {
      videoSprite.bitmap.onFormatSetup.add(function() {
        videoSprite.setGraphicSize(FlxG.width, FlxG.height);
        videoSprite.updateHitbox();
        videoSprite.screenCenter();
      });
    }

    // start video and adjust resolution to screen size
    videoSprite.load(videoName, shouldLoop ? ['input-repeat=65545'] : null);
    _videos.push(this);
  }

  var alreadyDestroyed:Bool = false;

  override function destroy()
  {
    if (alreadyDestroyed) return;

    Debug.logInfo('Video destroyed');
    if (this.cover != null)
    {
      this.remove(this.cover);
      this.cover.destroy();
    }

    if (this.finishCallback != null) finishCallback();
    onSkip = null;

    if (FlxG.state != null)
    {
      if (FlxG.state.members.contains(this)) FlxG.state.remove(this);
      if (FlxG.state.subState != null && FlxG.state.subState.members.contains(this)) FlxG.state.subState.remove(this);
    }
    super.destroy();
    alreadyDestroyed = true;
    _videos.remove(this);
  }

  override function update(elapsed:Float)
  {
    if (canSkip)
    {
      if (Controls.instance.pressed('accept')) holdingTime = Math.max(0, Math.min(_timeToSkip, holdingTime + elapsed));
      else if (holdingTime > 0) holdingTime = Math.max(0, FlxMath.lerp(holdingTime, -0.1, FlxMath.bound(elapsed * 3, 0, 1)));
      updateSkipAlpha();

      if (holdingTime >= _timeToSkip)
      {
        if (onSkip != null) onSkip();
        finishCallback = null;
        videoSprite.bitmap.onEndReached.dispatch();
        Debug.logInfo('Skipped video');
        return;
      }
    }
    super.update(elapsed);
  }

  function set_canSkip(newValue:Bool)
  {
    canSkip = newValue;
    if (canSkip && skipSprite == null)
    {
      skipSprite = new FlxPieDial(0, 0, 40, FlxColor.WHITE, 40, true, 24);
      skipSprite.replaceColor(FlxColor.BLACK, FlxColor.TRANSPARENT);
      skipSprite.x = FlxG.width - (skipSprite.width + 80);
      skipSprite.y = FlxG.height - (skipSprite.height + 72);
      skipSprite.amount = 0;
      add(skipSprite);
    }
    else if (skipSprite != null)
    {
      remove(skipSprite);
      skipSprite.destroy();
      skipSprite = null;
    }
    return canSkip;
  }

  function updateSkipAlpha()
  {
    if (skipSprite == null) return;

    skipSprite.amount = Math.min(1, Math.max(0, (holdingTime / _timeToSkip) * 1.025));
    skipSprite.alpha = FlxMath.remapToRange(skipSprite.amount, 0.025, 1, 0, 1);
  }

  public function play()
  {
    isPlaying = true;
    videoSprite?.play();
  }

  public function resume()
  {
    isPaused = false;
    videoSprite?.resume();
  }

  public function pause()
  {
    isPaused = true;
    videoSprite?.pause();
  }
}
#else
class VideoSprite extends FlxSpriteGroup {}
#end
