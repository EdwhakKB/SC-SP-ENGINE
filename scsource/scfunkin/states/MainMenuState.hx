package scfunkin.states;

import flixel.FlxObject;
import flixel.addons.transition.FlxTransitionableState;
import flixel.addons.display.FlxBackdrop;
import flixel.addons.display.FlxGridOverlay;
import flixel.effects.FlxFlicker;
import flixel.input.keyboard.FlxKey;
import flixel.input.mouse.FlxMouseEvent;
import lime.app.Application;
import scfunkin.states.MusicBeatState.subStates;
import scfunkin.states.MusicBeatState;
import scfunkin.backend.data.WeekData;
import scfunkin.states.editors.MasterEditorMenu;
import scfunkin.states.substates.options.OptionsState;
import scfunkin.objects.ui.Character;

enum MainMenuColumn
{
  LEFT;
  CENTER;
  RIGHT;
}

class FlxMenuSprite extends FlxSprite
{
  // A sort of tag
  public var item:String = '';
}

class MainMenuState extends MusicBeatState
{
  public static var SCEVersion:String = '1.5.3'; // This is also used for Discord RPC
  public static var curSelected:Int = 0;
  public static var curColumn:MainMenuColumn = CENTER;

  public static var freakyPlaying:Bool = false;

  // Centered/Text Options
  final optionShit:Array<String> = ['story_mode', 'freeplay', #if MODS_ALLOWED 'mods', #end 'credits'];

  var allowMouse:Bool = true; // Turn this off to block mouse movement in menus

  var menuItems:FlxTypedGroup<FlxMenuSprite>;
  var leftItem:FlxMenuSprite;
  var rightItem:FlxMenuSprite;

  var leftOption:String = #if ACHIEVEMENTS_ALLOWED 'achievements' #else null #end;
  var rightOption:String = 'options';

  var magenta:FlxSprite;

  var bg:FlxSprite;
  var camFollow:FlxObject;
  var camFollowPos:FlxObject;

  var grid:FlxBackdrop;

  override function create()
  {
    #if MODS_ALLOWED
    Mods.pushGlobalMods();
    #end
    Mods.loadTopMod();

    #if DISCORD_ALLOWED
    // Updating Discord Rich Presence
    DiscordClient.changePresence("Waiting for an menu option - Main Menu", null);
    #end

    Conductor.bpm = 128.0;

    persistentUpdate = persistentDraw = true;

    FlxG.mouse.visible = true;

    bg = new FlxSprite(0, 0).loadGraphic(Paths.image('menuBG'));
    bg.antialiasing = Save.get('antialiasing');
    bg.scrollFactor.set();
    bg.alpha = 0.5;
    // bg.setGraphicSize(FlxG.width * 2, FlxG.height * 2);
    bg.updateHitbox();
    bg.screenCenter();
    add(bg);

    camFollow = new FlxObject(0, 0, 1, 1);
    camFollowPos = new FlxObject(0, 0, 1, 1);
    add(camFollow);
    add(camFollowPos);

    magenta = new FlxSprite(0, 0).loadGraphic(Paths.image('menuDesat'));
    magenta.antialiasing = Save.get('antialiasing');
    magenta.scrollFactor.set();
    magenta.alpha = 0.5;
    // magenta.setGraphicSize(Std.int(bg.width * 4), Std.int(bg.height * 4));
    // magenta.setGraphicSize(FlxG.width * 2, FlxG.height * 2);
    magenta.updateHitbox();
    magenta.screenCenter();
    magenta.visible = false;
    magenta.color = 0xFFfd719b;
    add(magenta);

    // magenta.scrollFactor.set();

    grid = new FlxBackdrop(FlxGridOverlay.createGrid(80, 80, 160, 160, true, 0x33FFFFFF, 0x0));
    grid.velocity.set(FlxG.random.bool(50) ? 90 : -90, FlxG.random.bool(50) ? 90 : -90);
    grid.alpha = 0;
    FlxTween.tween(grid, {alpha: 0.56}, 0.5, {ease: FlxEase.quadOut});
    if (Save.isQuality('high', '>=')) add(grid);

    menuItems = new FlxTypedGroup<FlxMenuSprite>();
    add(menuItems);

    for (num => option in optionShit)
    {
      var item:FlxMenuSprite = createMenuItem(option, 60 * num, (num * 140) + 90);
      item.y += (4 - optionShit.length) * 70; // Offsets for when you have anything other than 4 items
      item.screenCenter(X);
    }

    if (rightOption != null)
    {
      rightItem = createMenuItem(rightOption, FlxG.width - 60, 490, true);
      rightItem.x -= rightItem.width;
    }

    if (leftOption != null) leftItem = createMenuItem(leftOption, 60, 490, true);

    final sceVersion:FlxText = new FlxText(12, FlxG.height - 64, 0, "SCE v" + SCEVersion, 16);
    sceVersion.active = false;
    sceVersion.scrollFactor.set();
    sceVersion.borderStyle = FlxTextBorderStyle.OUTLINE_FAST;
    sceVersion.borderColor = FlxColor.BLACK;
    sceVersion.font = Paths.font('vcr.ttf');
    add(sceVersion);
    final fnfVersion:FlxText = new FlxText(12, FlxG.height - 24, 0, "Friday Night Funkin' v0.6.4", 16);
    fnfVersion.active = false;
    fnfVersion.scrollFactor.set();
    fnfVersion.borderStyle = FlxTextBorderStyle.OUTLINE_FAST;
    fnfVersion.borderColor = FlxColor.BLACK;
    fnfVersion.font = Paths.font('vcr.ttf');
    add(fnfVersion);

    // NG.core.calls.event.logEvent('swag').send();

    changeItem(0);

    #if ACHIEVEMENTS_ALLOWED
    // Unlocks "Freaky on a Friday Night" achievement if it's a Friday and between 18:00 PM and 23:59 PM
    if (Date.now().getDay() == 5 && Date.now().getHours() >= 18) Achievements.unlock('friday_night_play');
    #if MODS_ALLOWED
    Achievements.reloadList();
    #end
    #end

    super.create();

    FlxG.camera.follow(camFollowPos, null, 0.15);
  }

  function createMenuItem(name:String, x:Float, y:Float, looping:Bool = false):FlxMenuSprite
  {
    var menuItem:FlxMenuSprite = new FlxMenuSprite(x, y);
    menuItem.frames = Paths.getSparrowAtlas('mainmenu/menu_$name');
    menuItem.animation.addByPrefix('idle', '$name idle', 24, true);
    menuItem.animation.addByPrefix('selected', '$name selected', 24, !looping);
    menuItem.animation.play('idle');
    menuItem.updateHitbox();

    menuItem.antialiasing = Save.get('antialiasing');
    menuItem.scrollFactor.set();
    menuItem.item = name;
    menuItems.add(menuItem);
    return menuItem;
  }

  var selectedSomethin:Bool = false;

  var timeNotMoving:Float = 0;

  override function update(elapsed:Float)
  {
    if (FlxG.sound.music != null)
    {
      if (FlxG.sound.music.volume < 0.8)
      {
        FlxG.sound.music.volume += 0.5 * elapsed;
      }
      Conductor.songPosition = FlxG.sound.music.time;
    }

    var lerpVal:Float = scfunkin.utils.MathUtil.clamp(elapsed * 7.5, 0, 1);
    camFollowPos.setPosition(FlxMath.lerp(camFollowPos.x, camFollow.x, lerpVal), FlxMath.lerp(camFollowPos.y, camFollow.y, lerpVal));

    for (i in [bg, magenta])
    {
      var mult:Float = FlxMath.lerp(1, i.scale.x, scfunkin.utils.MathUtil.clamp(1 - (elapsed * 9), 0, 1));
      i.scale.set(mult, mult);
      i.updateHitbox();
      i.offset.set();
    }

    if (!selectedSomethin)
    {
      if (FlxG.mouse.wheel != 0)
      {
        FlxG.sound.play(Paths.sound('scrollMenu'), 0.4);
        changeItem(-FlxG.mouse.wheel);
      }

      if (controls.UI_UP_P || controls.UI_DOWN_P)
      {
        FlxG.sound.play(Paths.sound('scrollMenu'));
        changeItem(controls.UI_UP_P ? -1 : 1);
      }

      var allowMouse:Bool = allowMouse;
      if (allowMouse
        && ((FlxG.mouse.deltaViewX != 0 && FlxG.mouse.deltaViewY != 0)
          || FlxG.mouse.justPressed)) // FlxG.mouse.deltaViewX/Y checks is more accurate than FlxG.mouse.justMoved
      {
        allowMouse = false;
        FlxG.mouse.visible = true;
        timeNotMoving = 0;

        var selectedItem:FlxMenuSprite;
        switch (curColumn)
        {
          case CENTER:
            selectedItem = menuItems.members[curSelected];
          case LEFT:
            selectedItem = leftItem;
          case RIGHT:
            selectedItem = rightItem;
        }

        if (leftItem != null && FlxG.mouse.overlaps(leftItem))
        {
          allowMouse = true;
          if (selectedItem != leftItem)
          {
            curColumn = LEFT;
            changeItem();
          }
        }
        else if (rightItem != null && FlxG.mouse.overlaps(rightItem))
        {
          allowMouse = true;
          if (selectedItem != rightItem)
          {
            curColumn = RIGHT;
            changeItem();
          }
        }
        else
        {
          var dist:Float = -1;
          var distItem:Int = -1;
          for (i in 0...optionShit.length)
          {
            var memb:FlxSprite = menuItems.members[i];
            if (FlxG.mouse.overlaps(memb))
            {
              var distance:Float = Math.sqrt(Math.pow(memb.getGraphicMidpoint().x - FlxG.mouse.viewX, 2)
                + Math.pow(memb.getGraphicMidpoint().y - FlxG.mouse.viewY, 2));
              if (dist < 0 || distance < dist)
              {
                dist = distance;
                distItem = i;
                allowMouse = true;
              }
            }
          }

          if (distItem != -1 && selectedItem != menuItems.members[distItem])
          {
            curColumn = CENTER;
            curSelected = distItem;
            changeItem();
          }
        }
      }
      else
      {
        timeNotMoving += elapsed;
        if (timeNotMoving > 2) FlxG.mouse.visible = false;
      }

      switch (curColumn)
      {
        case CENTER:
          if (controls.UI_LEFT_P && leftOption != null)
          {
            curColumn = LEFT;
            changeItem();
          }
          else if (controls.UI_RIGHT_P && rightOption != null)
          {
            curColumn = RIGHT;
            changeItem();
          }

        case LEFT:
          if (controls.UI_RIGHT_P)
          {
            curColumn = CENTER;
            changeItem();
          }

        case RIGHT:
          if (controls.UI_LEFT_P)
          {
            curColumn = CENTER;
            changeItem();
          }
      }

      if (controls.BACK)
      {
        selectedSomethin = true;
        FlxG.sound.play(Paths.sound('cancelMenu'));
        MusicBeatState.switchState(new TitleState());
      }

      if (controls.ACCEPT || (FlxG.mouse.justPressed && allowMouse) #if android || FlxG.android.justPressed.BACK #end)
      {
        FlxG.sound.play(Paths.sound('confirmMenu'));
        if (optionShit[curSelected] != 'donate')
        {
          FlxG.mouse.visible = false;
          selectedSomethin = true;

          if (Save.get('flashing')) FlxFlicker.flicker(magenta, 1.1, 0.15, false);

          var item:FlxMenuSprite;
          var option:String;
          switch (curColumn)
          {
            case CENTER:
              option = optionShit[curSelected];
              item = menuItems.members[curSelected];

            case LEFT:
              option = leftOption;
              item = leftItem;

            case RIGHT:
              option = rightOption;
              item = rightItem;
          }

          FlxFlicker.flicker(item, 1, 0.06, false, false, function(flick:FlxFlicker) {
            switch (option)
            {
              case 'story_mode':
                MusicBeatState.switchState(new scfunkin.states.menu.StoryMenuState());
              case 'freeplay':
                MusicBeatState.switchState(new scfunkin.states.freeplay.FreeplayState());
              #if MODS_ALLOWED
              case 'mods':
                MusicBeatState.switchState(new scfunkin.states.menu.ModsMenuState());
              #end

              #if ACHIEVEMENTS_ALLOWED
              case 'achievements':
                MusicBeatState.switchState(new scfunkin.states.menu.AchievementsMenuState());
              #end

              case 'credits':
                MusicBeatState.switchState(new scfunkin.states.menu.CreditsState());
              case 'options':
                MusicBeatState.switchState(new scfunkin.states.substates.options.OptionsState());
                OptionsState.onPlayState = false;
                if (PlayState.SONG != null)
                {
                  PlayState.SONG.getSongData('options').arrowSkin = null;
                  PlayState.SONG.getSongData('options').splashSkin = null;
                  PlayState.SONG.getSongData('options').strumSkin = null;
                  PlayState.SONG.getSongData('options').holdCoverSkin = null;
                  PlayState.stageUI = 'normal';
                }
            }
          });

          itemsEffect(item, option);
        }
        else
          scfunkin.utils.CoolUtil.browserLoad('https://ninja-muffin24.itch.io/funkin');
      }
      #if desktop
      else if (controls.justPressed('debug_1'))
      {
        selectedSomethin = true;
        FlxG.mouse.visible = false;
        MusicBeatState.switchState(new MasterEditorMenu());
      }
      else if (FlxG.keys.justPressed.ZERO)
      {
        selectedSomethin = true;
        FlxG.mouse.visible = false;
        MusicBeatState.switchState(new TestingState());
      }
      #end
    }

    super.update(elapsed);
  }

  function itemsEffect(selectedItem:FlxMenuSprite, chosen:String)
  {
    for (memb in menuItems)
    {
      if (memb == selectedItem) continue;
      switch (chosen)
      {
        case 'story_mode':
          switch (memb.item)
          {
            case 'freeplay':
              FlxTween.tween(memb, {y: memb.y + 1000}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #if MODS_ALLOWED
            case 'mods':
              FlxTween.tween(memb, {y: memb.y + 700}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
            case 'credits':
              FlxTween.tween(memb, {y: memb.y + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            case 'options':
              FlxTween.tween(memb, {x: memb.x + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #if ACHIEVEMENTS_ALLOWED
            case 'achievements':
              FlxTween.tween(memb, {x: memb.x - 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
          }
        case 'freeplay':
          switch (memb.item)
          {
            case 'story_mode':
              FlxTween.tween(memb, {y: memb.y - 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #if MODS_ALLOWED
            case 'mods':
              FlxTween.tween(memb, {y: memb.y + 700}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
            case 'credits':
              FlxTween.tween(memb, {y: memb.y + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #if ACHIEVEMENTS_ALLOWED
            case 'achievements':
              FlxTween.tween(memb, {x: memb.x - 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
            case 'options':
              FlxTween.tween(memb, {x: memb.x + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
          }
        #if MODS_ALLOWED
        case 'mods':
          switch (memb.item)
          {
            case 'story_mode':
              FlxTween.tween(memb, {y: memb.y - 600}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            case 'freeplay':
              FlxTween.tween(memb, {y: memb.y - 900}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            case 'credits':
              FlxTween.tween(memb, {y: memb.y + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #if ACHIEVEMENTS_ALLOWED
            case 'achievements':
              FlxTween.tween(memb, {x: memb.x - 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
            case 'options':
              FlxTween.tween(memb, {x: memb.x + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
          }
        #end
        case 'credits':
          switch (memb.item)
          {
            case 'story_mode':
              FlxTween.tween(memb, {y: memb.y - 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            case 'freeplay':
              FlxTween.tween(memb, {y: memb.y - 700}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #if MODS_ALLOWED
            case 'mods':
              FlxTween.tween(memb, {y: memb.y - 1000}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
            #if ACHIEVEMENTS_ALLOWED
            case 'achievements':
              FlxTween.tween(memb, {x: memb.x - 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
            case 'options':
              FlxTween.tween(memb, {x: memb.x + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
          }
        case 'options':
          switch (memb.item)
          {
            #if ACHIEVEMENTS_ALLOWED
            case 'achievements':
              FlxTween.tween(memb, {x: memb.x - 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            #end
            default:
              FlxTween.tween(memb, {y: memb.y + 1800}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
          }
        #if ACHIEVEMENTS_ALLOWED
        case 'achievements':
          switch (memb.item)
          {
            case 'options':
              FlxTween.tween(memb, {x: memb.x + 300}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
            default:
              FlxTween.tween(memb, {y: memb.y + 1800}, 0.6, {ease: scfunkin.utils.EaseUtil.bell});
          }
        #end
      }
    }
  }

  override function beatHit()
  {
    super.beatHit();

    bg.scale.set(1.06, 1.06);
    bg.updateHitbox();
    bg.offset.set();

    FlxTween.tween(bg, {alpha: 0.7}, Conductor.crochet / 1900,
      {
        onComplete: function(flxT:FlxTween) {
          FlxTween.tween(bg, {alpha: 0.4}, Conductor.crochet / 1900);
        }
      });
  }

  function changeItem(change:Int = 0)
  {
    if (change != 0) curColumn = CENTER;
    curSelected = FlxMath.wrap(curSelected + change, 0, optionShit.length - 1);
    FlxG.sound.play(Paths.sound('scrollMenu'));

    for (item in menuItems)
    {
      item.animation.play('idle');
      item.centerOffsets();
    }

    var selectedItem:FlxMenuSprite;
    switch (curColumn)
    {
      case CENTER:
        selectedItem = menuItems.members[curSelected];
      case LEFT:
        selectedItem = leftItem;
      case RIGHT:
        selectedItem = rightItem;
    }
    selectedItem.animation.play('selected');
    selectedItem.centerOffsets();

    switch (selectedItem.item)
    {
      case 'story_mode':
        camFollow.y = selectedItem.getGraphicMidpoint().y - 70;
      case 'freeplay':
        camFollow.y = selectedItem.getGraphicMidpoint().y + 40;
      #if MODS_ALLOWED
      case 'mods':
        camFollow.y = selectedItem.getGraphicMidpoint().y + 70;
      #end
      case 'credits':
        camFollow.y = selectedItem.getGraphicMidpoint().y + 420;
      #if ACHIEVEMENTS_ALLOWED
      case 'achievements':
        camFollow.y = selectedItem.getGraphicMidpoint().y + 30;
        camFollow.x = selectedItem.getGraphicMidpoint().x + 70;
      #end
      case 'options':
        camFollow.y = selectedItem.getGraphicMidpoint().y + 30;
        camFollow.x = selectedItem.getGraphicMidpoint().x - 90;
    }
  }
}

class OptionsDirect extends MusicBeatState
{
  var menuBG:FlxSprite;

  public static var instance:OptionsDirect = null;

  var colorArray:Array<FlxColor> = [
    FlxColor.fromRGB(148, 0, 211),
    FlxColor.fromRGB(75, 0, 130),
    FlxColor.fromRGB(0, 0, 255),
    FlxColor.fromRGB(0, 255, 0),
    FlxColor.fromRGB(255, 255, 0),
    FlxColor.fromRGB(255, 127, 0),
    FlxColor.fromRGB(255, 0, 0)
  ];

  override function create()
  {
    instance = this;
    FlxG.camera.fade(FlxColor.BLACK, 0.8, true);
    Paths.clearStoredMemory();
    Paths.clearUnusedMemory();
    subStates.push(new scfunkin.states.substates.options.OptionsMenu());
    menuBG = new FlxSprite().loadGraphic(Paths.image("menuDesat"));
    menuBG.color = 0xFFea71fd;
    menuBG.setGraphicSize(Std.int(menuBG.width * 1.1));
    menuBG.updateHitbox();
    menuBG.screenCenter();
    menuBG.antialiasing = Save.get('antialiasing');
    add(menuBG);

    tweenColorShit();

    super.create();

    openSubState(subStates[0]);
  }

  function tweenColorShit()
  {
    var beforeInt = FlxG.random.int(0, 6);
    var randomInt = FlxG.random.int(0, 6);

    FlxTween.color(menuBG, 4, menuBG.color, colorArray[beforeInt],
      {
        onComplete: function(twn) {
          if (beforeInt != randomInt) beforeInt = randomInt;

          tweenColorShit();
        }
      });
  }
}
